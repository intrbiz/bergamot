options {
  STATIC = false;
  UNICODE_INPUT = true;
}

PARSER_BEGIN(VirtualCheckExpressionParserInternal)

package com.intrbiz.bergamot.virtual.parser;

import com.intrbiz.bergamot.model.*;
import com.intrbiz.bergamot.virtual.operator.*;
import com.intrbiz.bergamot.virtual.reference.*;
import com.intrbiz.bergamot.virtual.*;
import java.util.*;

@SuppressWarnings("all")
public final class VirtualCheckExpressionParserInternal {

}

PARSER_END(VirtualCheckExpressionParserInternal)

SKIP : { " " | "\t" | "\n" | "\r" | "\r\n" }
TOKEN : 
{
    <UUID               :  (<HEXCHAR>){8} "-" (<HEXCHAR>){4} "-" (<HEXCHAR>){4} "-" (<HEXCHAR>){4} "-" (<HEXCHAR>){12} >
  | <XOR                 : "^" | "xor" >
  | <OR                  : "||" | "|" >
  | <LOR                 : "or" >
  | <AND                 : "&&" | "&" | "and" >
  | <NOT                 : "!" | "not" >
  | <EQ                  : "==" | "eq" >
  | <NE                  : "!=" | "ne" >
  | <LT                  : "<"  | "lt" >
  | <LTEQ                : "<=" | "lteq" >
  | <GT                  : ">"  | "gt" >
  | <GTEQ                : ">=" | "gteq" >
  | <STCOMP              : "(" >
  | <EDCOMP              : ")" >
  | <STARRAY             : "[" >
  | <EDARRAY             : "]" >
  | <STBRACE             : "{" >
  | <EDBRACE             : "}" >
  | <SEP                 : "," >
  | <HOST                : "host" >
  | <SERVICE             : "service" >
  | <TRAP                : "trap" >
  | <HOSTS               : "hosts" >
  | <SERVICES            : "services" >
  | <TRAPS               : "traps" >
  | <RESOURCE            : "resource" >
  | <CLUSTER             : "cluster" >
  | <POOL                : "pool" >
  | <ON                  : "on" >
  | <IN                  : "in" >
  | <COUNT               : "count" >
  | <IS                  : "is" >
  | <ALL                 : "all" >
  | <ANY                 : "any" >
  | <ONE                 : "one" >
  | <TWO                 : "two" >
  | <THREE               : "three" >
  | <FOUR                : "four" >
  | <FIVE                : "five" >
  | <SIX                 : "six" >
  | <SEVEN               : "seven" >
  | <EIGHT               : "eight" >
  | <NINE                : "nine" >
  | <LMORE               : "more" >
  | <LLESS               : "less" >
  | <OF                  : "of" >
  | <CASE                : "case" >
  | <WHEN                : "when" >
  | <THEN                : "then" >  
  | <ELSE                : "else" >
  | <END                 : "end" >
  | <AS                  : "as" >
  | <MAJORITY            : "majority" >
  | <QUORUM              : "quorum" >
  | <STATUS              : "PENDING" | "OK" | "WARNING" | "CRITICAL" | "UNKNOWN" | "TIMEOUT" | "DISCONNECTED" | "ACTION" >
  | <BOOLEAN             : "true" | "TRUE" | "false" | "FALSE" >
  | <INTEGER             : (["0"-"9"])+ >
  | <#HEXCHAR            : ["a"-"f","A"-"F","0"-"9"]> 
  | <#ESCAPECHAR         : "\\" ["n","t","b","r","f","\\","'","\"", "/"] > 
  | <#UESCAPECHAR        : "\\u" <HEXCHAR><HEXCHAR><HEXCHAR><HEXCHAR> > 
  | <STRING              : "\'" ( (~["\'","\\","\n","\r"]) | <ESCAPECHAR> | <UESCAPECHAR>)* "\'" >
  | <QUOTE               : "\"" > : STRINGSTATE 
}

<STRINGSTATE> MORE  : { "\\" : ESCSTATE }
<STRINGSTATE> TOKEN : { <ENDQUOTE : <QUOTE> > : DEFAULT | <CHAR : ~["\"","\\"]> }
<ESCSTATE> TOKEN : { <CNTRLESC : ["\'","\\","/","b","f","n","r","t"]> : STRINGSTATE }
<ESCSTATE> MORE  : { "u" : HEXSTATE }
<HEXSTATE> TOKEN : { <#HEX : ["a"-"f","A"-"F","0"-"9"]> | <HEXESC : <HEX><HEX><HEX><HEX> > : STRINGSTATE }

private Status readStatus() : 
{
    Token t;
}
{
    t = <STATUS>
    { return Status.parse(t.image); }
}

private UUID readUUID() : 
{
    Token t;
}
{
    t = <UUID>
    { return java.util.UUID.fromString(t.image); }
}

private String readString() : 
{
    Token t;
}
{
    t = <STRING>
    { return t.image.substring(1, t.image.length() -1); }
}

private ValueOperator readHost() :
{
    String name = null;
    UUID id = null;
    HostReference host = null;
}
{
    ( 
		<HOST>
		(
	    	(
				id = readUUID()
				{ host = new HostById(id); }
	    	) |
	    	(
				name = readString()
				{ host = new HostByName(name); }
	    	)
		)
    )
    { return new ValueOperator(host); }
}

private ValueOperator readService() :
{
    String name = null;
    String hostName = null;
    UUID id = null;
    ServiceReference service = null;
    HostReference host = null;
}
{
    ( 
		<SERVICE>
		(
	    	(
				id = readUUID()
				{ service = new ServiceById(id); }
	    	) |
	    	(
				name = readString()
	    		<ON>
	    		(<HOST>)?
	    		(
			    	(
						id = readUUID()
						{ host = new HostById(id); }
			    	) |
			    	(
						hostName = readString()
						{ host = new HostByName(hostName); }
			    	)
				)
	      		{ service = new ServiceByName(host, name); }
	    	)
		)
    )
    { return new ValueOperator(service); }
}

private ValueOperator readParentService() :
{
    String name = null;
    String hostName = null;
    UUID id = null;
    ServiceReference service = null;
    HostReference host = null;
}
{
    ( 
		<SERVICE>
		(
	    	(
				id = readUUID()
				{ service = new ServiceById(id); }
	    	) |
	    	LOOKAHEAD(2) (
	    		name = readString()
	    		<ON>
	    		(<HOST>)?
	    		(
			    	(
						id = readUUID()
						{ host = new HostById(id); }
			    	) |
			    	(
						hostName = readString()
						{ host = new HostByName(hostName); }
			    	)
				)
	      		{ service = new ServiceByName(host, name); }
	    	) |
	    	(
				name = readString()
	    		{ service = new AnonymousService(name); }
	    	)
		)
    )
    { return new ValueOperator(service); }
}

private ValueOperator readTrap() :
{
    String name = null;
    String hostName = null;
    UUID id;
    TrapReference trap = null;
    HostReference host = null;
}
{
    ( 
		<TRAP>
		(
	    	(
				id = readUUID()
				{ trap = new TrapById(id); }
	    	) |
	    	(
				name = readString()
	    		<ON>
	    		(<HOST>)?
				(
			    	(
						id = readUUID()
						{ host = new HostById(id); }
			    	) |
			    	(
						hostName = readString()
						{ host = new HostByName(hostName); }
			    	)
				)
	      		{ trap = new TrapByName(host, name); }
	    	)
		)
    )
    { return new ValueOperator(trap); }
}

private ValueOperator readParentTrap() :
{
    String name = null;
    String hostName = null;
    UUID id;
    TrapReference trap = null;
    HostReference host = null;
}
{
    ( 
		<TRAP>
		(
	    	(
				id = readUUID()
				{ trap = new TrapById(id); }
	    	) |
	    	LOOKAHEAD(2) (
				name = readString()
	    		<ON>
	    		(<HOST>)?
				(
			    	(
						id = readUUID()
						{ host = new HostById(id); }
			    	) |
			    	(
						hostName = readString()
						{ host = new HostByName(hostName); }
			    	)
				)
	      		{ trap = new TrapByName(host, name); }
	    	) |
	    	(
	    		name = readString()
	    		{ trap = new AnonymousTrap(name); }
	    	)
		)
    )
    { return new ValueOperator(trap); }
}

private ValueOperator readCluster() :
{
    String name = null;
    UUID id = null;
    ClusterReference cluster = null;
}
{
    ( 
		<CLUSTER>
		(
	    	(
				id = readUUID()
				{ cluster = new ClusterById(id); }
	    	) |
	    	(
				name = readString()
				{ cluster = new ClusterByName(name); }
	    	)
		)
    )
    { return new ValueOperator(cluster); }
}

private ValueOperator readResource() :
{
    String name = null;
    String clusterName = null;
    UUID id = null;
    ClusterReference cluster = null;
    ResourceReference resource = null;
}
{
    ( 
		<RESOURCE>
		(
	    	(
				id = readUUID()
				{ resource = new ResourceById(id); }
	    	) |
	    	(
				name = readString()
				(
		    		<ON>
		    		(<CLUSTER>)?
		    		(
				    	(
							id = readUUID()
							{ cluster = new ClusterById(id); }
				    	) |
				    	(
							name = readString()
							{ cluster = new ClusterByName(name); }
				    	)
					)
		      		{ resource = new ResourceByName(cluster, name); }
				)?
	    	)
		)
    )
    { return new ValueOperator(null); }
}

private ValueOperator readCheck() :
{
    ValueOperator check;
}
{
    ( 
    	check = readHost()          |
    	check = readService()       |
    	check = readTrap()          |
    	check = readCluster()       |
    	check = readResource()
    )
    { return check; }
}

private ListValue readCheckList() :
{
    List<ValueOperator> checks = new LinkedList<ValueOperator>();
    ValueOperator check;
}
{
    (
    	<STARRAY>
    	(
    		check = readCheck()
    		{ checks.add(check); }
    		(
    			<SEP>
    			check = readCheck()
    			{ checks.add(check); }
    		)*
    	)?
    	<EDARRAY>
    )
    { return new ListValue(checks); }
}

private ValuesOperator readHostPool() :
{
    String pool;
}
{
    (
    	<HOSTS>
    	<IN>
    	(<RESOURCE>)?
    	<POOL>
    	pool = readString()
    )
    { return new PoolOperator(new HostPool(pool)); }
}

private ValuesOperator readServicePool() :
{
    String service;
    String pool;
}
{
    (
        <SERVICE>
        service = readString()
        <ON>
    	<HOSTS>
    	<IN>
    	(<RESOURCE>)?
    	<POOL>
    	pool = readString()
    )
    { return new PoolOperator(new ServicePool(service, pool)); }
}

private ValuesOperator readTrapPool() :
{
    String trap;
    String pool;
}
{
    (
        <TRAP>
        trap = readString()
        <ON>
    	<HOSTS>
    	<IN>
    	<POOL>
    	pool = readString()
    )
    { return new PoolOperator(new TrapPool(trap, pool)); }
}

private ValuesOperator readAnonymousServicePool() :
{
    String pool;
}
{
    (
        <SERVICES>
        <IN>
        (<RESOURCE>)?
        <POOL>
        pool = readString()
    )
    { return new PoolOperator(new AnonymousServicePool(pool)); }
}

private ValuesOperator readAnonymousTrapPool() :
{
    String pool;
}
{
    (
        <TRAPS>
        <IN>
        (<RESOURCE>)?
        <POOL>
        pool = readString()
    )
    { return new PoolOperator(new AnonymousTrapPool(pool)); }
}

private ValuesOperator readValues() :
{
    ValuesOperator values;
}
{
    (
    	values = readCheckList()             |
    	values = readHostPool()              |
    	values = readServicePool()           |
    	values = readTrapPool()              |
    	values = readAnonymousServicePool()  |
    	values = readAnonymousTrapPool()
    )
    { return values; }
}

public List<CheckReference<?>> readParents() :
{
    List<CheckReference<?>> checks = new LinkedList<CheckReference<?>>();
    ValueOperator check;
}
{
    (
		( 
	        check = readHost()             |
	        check = readCluster()          |
	        check = readResource()         |
	        check = readParentService()    |
    		check = readParentTrap()
		)
		{ checks.add(check.getCheck()); }
		(
			<SEP>
			( 
		        check = readHost()             |
		        check = readCluster()          |
		        check = readResource()         |
		        check = readParentService()    |
	    		check = readParentTrap()
			)
			{ checks.add(check.getCheck()); }
		)*
    )
    { return checks; }
}

private VirtualCheckOperator readValue() :
{
    VirtualCheckOperator value;
}
{
    (
		(
	    	<STCOMP> 
	    	value = readExpression() 
	    	<EDCOMP> 
	    	{ value = new BracketOperator(value); }
		) |
		LOOKAHEAD(readCheck())   value = readCheck()
    )
    { return value; }
}

private VirtualCheckOperator readUnary() :
{
    VirtualCheckOperator value;
}
{
    (
        <NOT>
        value = readValue()
    )
    { return new NotOperator(value); }
}

private VirtualCheckOperator readAllFunction() :
{
    ValuesOperator values;
}
{
    (
        <ALL>
        (<OF>)?
        values = readValues()
    )
    { return new AllFunction(values); }
}

private VirtualCheckOperator readAnyFunction() :
{
    ValuesOperator values;
}
{
    (
        <ANY>
        (<OF>)?
        values = readValues()
    )
    { return new AnyFunction(values); }
}

private VirtualCheckOperator readOneOfFunction() :
{
    Token x;
    ValuesOperator values;
    Status as = null;
    Token type = null;
}
{
    (
        (
            x = <ONE>   |
            x = <TWO>   |
            x = <THREE> |
            x = <FOUR>  |
            x = <FIVE>  |
            x = <SIX>   |
            x = <SEVEN> |
            x = <EIGHT> |
            x = <NINE>
        )
        (
            <LOR>
            (
                type = <LMORE> |
                type = <LLESS>
            )
        )?
        (<OF>)?
        values = readValues()
        (
        	<AS>
        	as = readStatus()
        )?
    )
    { 
        if (type != null)
        {
            if (LMORE == type.kind)
            {
                return new OneOrMoreOfFunction(values, x.image, as); 
            } 
            else if (LLESS == type.kind) 
            {
                return new OneOrLessOfFunction(values, x.image, as); 
            }
        } 
        else 
        {
            return new OneOfFunction(values, x.image, as); 
        }
    }
}

private VirtualCheckOperator readMajorityOfFunction() :
{
    ValuesOperator values;
    Status warningAs = null;
    Status criticalAs = null;
    Token type = null;
}
{
    (
    	(
        	<MAJORITY> |
        	<QUORUM>
        )
        (<OF>)?
        values = readValues()
        (
        	<AS>
        	warningAs = readStatus()
        	<SEP>
        	criticalAs = readStatus()
        )?
    )
    { 
        return new MajorityOfFunction(values, warningAs, criticalAs); 
    }
}

private VirtualCheckOperator readCountFunction() :
{
    Status status = null;
    ValuesOperator values;
    Token test = null;
    Token value = null;
    Status as = null;
}
{
    (
        <COUNT>
        (
        	status = readStatus()
        )?
        (<OF>)?
        values = readValues()
        (<IS>)?
        (
        	test = <EQ>    | 
        	test = <NE>    | 
        	test = <LT>    | 
        	test = <LTEQ>  | 
        	test = <GT>    | 
        	test = <GTEQ>
        )
        value = <INTEGER>
        (
        	<AS>
        	as = readStatus()
        )?
    )
    { return new CountFunction(status, values, test.image.toLowerCase(), Integer.parseInt(value.image), as); }
}

private VirtualCheckOperator readCaseFunction() :
{
	VirtualCheckOperator check;
	Status status;
	Status as;
	List<CaseOperator.CaseWhen> when = new LinkedList<CaseOperator.CaseWhen>();
	Status elseStatus = null;
}
{
    (
        <CASE>
        (
			<WHEN>
			check = readExpression()
			<IS>
			status = readStatus()
			<THEN>
			as = readStatus()
			{ when.add(new CaseOperator.CaseWhen(check, status, as)); }
        )
        (
        	<WHEN>
			check = readExpression()
			<IS>
			status = readStatus()
			<THEN>
			as = readStatus()
			{ when.add(new CaseOperator.CaseWhen(check, status, as)); }
        )*
        (
        	<ELSE>
        	elseStatus = readStatus()
        )?
        <END>
    )
    { return new CaseOperator(when, elseStatus); }
}

private VirtualCheckOperator readFunction() :
{
    VirtualCheckOperator operator;
}
{
    (
        operator = readAllFunction()        |
        operator = readAnyFunction()        |
        operator = readOneOfFunction()      |
        operator = readMajorityOfFunction() |
        operator = readCountFunction()      |
        operator = readCaseFunction()
    )
    { return operator; }
}

private VirtualCheckOperator readOperator() :
{
    VirtualCheckOperator operator;
}
{
    (
        operator = readValue()    |
        operator = readUnary()    |
        operator = readFunction()
    )
    { return operator; }
}

public VirtualCheckOperator readExpression() :
{
    VirtualCheckOperator operator;
}
{
    (
		operator = readXorExpression()
    )
    { return operator; }
}

private VirtualCheckOperator readXorExpression() :
{
    VirtualCheckOperator l;
    VirtualCheckOperator r;
}
{
    (
		l = readOrExpression()
		(
	    	<XOR>
	    	r = readOrExpression()
	    	{ l = new XorOperator(l, r); }
		)*
    )
    { return l; }
}

private VirtualCheckOperator readOrExpression() :
{
    VirtualCheckOperator l;
    VirtualCheckOperator r;
}
{
    (
		l = readAndExpression()
		(
	    	(<OR> | <LOR>)
	    	r = readAndExpression()
	    	{ l = new OrOperator(l, r); }
		)*
    )
    { return l; }
}

private VirtualCheckOperator readAndExpression() :
{
    VirtualCheckOperator l;
    VirtualCheckOperator r;
}
{
    (
		l = readOperator()
		(
	    	<AND>
	    	r = readOperator()
	    	{l = new AndOperator(l,r); }
		)*
    )
    { return l; }
}
